import api.*

package api.auth XML PROPERTY factoryId 25 {
    properties tag;
    
    type resourceId                     is Unicode(255);    // ID ofa resource / permission
    
    enumset<int>    Permissionset       is OperationType;   // compact storage (2 bytes in Bonaparte compact format)
    enumset<String> PermissionsetAlpha  is OperationType;   // readable format
    
    enum UserLogLevelType {
        STEALTH,                // neither session nor any request
        SESSION_ONLY,           // only session statistics
        SESSION_WITH_COUNTS,    // sessions and updated access counts
        MESSAGE_ENTRY,          // message, but without serialized request and response
        REQUESTS,               // message, with serialized requests, but no responses
        FULL                    // requests and responses
    }
    
    /** A useful enum to access the map entries of a JWT token by instance.name(). */
    enum JwtFieldName {
        ISSUER                  = "iss",
        SUBJECT                 = "sub",        // user ID
        EXPIRATION              = "exp",
        NOT_BEFORE              = "nbf",
        ISSUED_AT               = "iat",
        JSON_TOKEN_IDENTIFIER   = "jti",
        NAME                    = "name",       // real name of the subject
        LOCALE                  = "locale",     // IETF BCP 47 language tag
        ZONEINFO                = "zoneinfo",   // time zone
        TENANT_ID               = "i",          // alphanumeric ID of the tenant
        TENANT_REF              = "t",          // numeric ID of the tenant (internal key)
        SESSION_REF             = "s",          // a unique reference for all contents of this token except iat, jti, exp (i.e. same for refreshes) 
        USER_REF                = "u",
        ROLE_REF                = "r",          // restriction to role
        SESSION_ID              = "o",          // constant for a sequence of sessions, i.e. multiple tokens of same session_id but different session_ref may have language or tenant changes
        QUOTA                   = "q",          // some (application specific) resource limitation, for example number of parallel requests, or total allowed CPU time
        LOG_LEVEL_OK            = "l",
        LOG_LEVEL_ERR           = "e",
        RESOURCE_FILTER         = "p",
        RESOURCE_IS_WILDCARD    = "w",
        PERMISSION_MIN          = "pl",
        PERMISSION_MAX          = "pu"
    }
    
    // JSON Web Token as per https://tools.ietf.org/html/rfc7519
    final class Jwt classId 200 {
        required (JwtAlg)               alg;
        required (JwtPayload...)        payload;
        required Binary(32)             sig;        // use a zero byte length signature for alg = none
    }
    
    final class JwtAlg classId 201 {
        required Ascii(3)               typ;        // "JWT"
        required Ascii(16)              alg;        // usually not more than 5 ("HS256"...), but more in case of encrpyted JWTs
        optional Ascii(3)               cty;        // content type
    }
    
    class JwtInfo classId 202 {
        optional Unicode(255)           issuer              properties tag = "iss";         // issuer
        optional Unicode(255)           userId              properties tag = "sub";         // subject (user ID)
        optional Unicode(255)           audience            properties tag = "aud";         // audience
        optional Instant                expiresAt           properties tag = "exp";         // expiration (time)
        optional Instant                notBefore           properties tag = "nbf";         // not before
        optional Instant                issuedAt            properties tag = "iat";         // issued at
        optional Unicode(40)            jsonTokenIdentifier properties tag = "jti";         // JSON token identifier (unique ID)
        
        // commonly used fields...
        optional Unicode(255)           name                properties tag = "name";        // user name (user real world name)
        optional languageTag            locale              properties tag = "locale";      // IETF BCP47 language tag (ISO639-A2, plus optional "-" and then ISO3166-A2)
        optional Ascii(64)              zoneinfo            properties tag = "zoneinfo";    // IANA 2015g or later time zone info string

        // fields used by this implementation... (references are internal numeric keys)
        optional genericId              tenantId            properties tag = "i";          // tenant ID
        optional Long                   tenantRef           properties tag = "t";          // tenant reference
        optional Long                   sessionRef          properties tag = "s";          // session reference
        optional Long                   userRef             properties tag = "u";          // user reference
        optional Long                   roleRef             properties tag = "r";          // role restriction / reference
        optional Uuid                   sessionId           properties tag = "o";          // alphanumeric (UUID type) session ID
        optional Integer                quota               properties tag = "q";          // resource quota
        optional Enum UserLogLevelType  logLevel            properties tag = "l";          // log level for messages processed OK
        optional Enum UserLogLevelType  logLevelErrors      properties tag = "e";          // log level for messages processed with error return (if different)
        optional resourceId             resource            properties tag = "p";          // permission resource restriction. If defined, then only access within / beneath this resource is allowed (if at all).
        optional Boolean                resourceIsWildcard  properties tag = "w";          // if TRUE, then the resource is also a positive ID: all resources matching or below "resource" are permitted, with "permissionsMin"
        optional Enumset Permissionset  permissionsMin      properties tag = "pl";         // minimum permissions (lower bound, set always allowed)
        optional Enumset Permissionset  permissionsMax      properties tag = "pu";         // maximum permissions (upper bound, permissions not in this set are never allowed)
    }
    
    // technical mapping
    class JwtPayload classId 203 {
        optional Unicode(255)           iss;        // issuer
        optional Unicode(255)           sub;        // subject (user ID)
        optional Unicode(255)           aud;        // audience
        optional Instant                exp;        // expiration (time)
        optional Instant                nbf;        // not before
        optional Instant                iat;        // issued at
        optional Unicode(40)            jti;        // JSON token identifier (unique ID)
        
        // commonly used fields...
        optional Unicode(255)           name;       // user name (user real world name)
        optional languageTag            locale;     // IETF BCP47 language tag (ISO639-A2, plus optional "-" and then ISO3166-A2)
        optional Ascii(64)              zoneinfo;   // IANA 2015g or later time zone info string

        // fields used by this implementation... (references are internal numeric keys)
        optional genericId              i;          // tenant ID
        optional Long                   t;          // tenant reference
        optional Long                   s;          // session reference
        optional Long                   u;          // user reference
        optional Long                   r;          // role restriction / reference
        optional Uuid                   o;          // alphanumeric (UUID type) session ID
        optional Integer                q;          // resource quota
        optional Enum UserLogLevelType  l;          // log level for messages processed OK
        optional Enum UserLogLevelType  e;          // log level for messages processed with error return (if different)
        optional resourceId             p;          // permission resource restriction. If defined, then only access within / beneath this resource is allowed (if at all).
        optional Boolean                w;          // if TRUE, then the resource is also a positive ID: all resources matching or below "resource" are permitted, with "permissionsMin"
        optional Enumset Permissionset  pl;         // minimum permissions (lower bound, set always allowed)
        optional Enumset Permissionset  pu;         // maximum permissions (upper bound, permissions not in this set are never allowed)
    }
}
